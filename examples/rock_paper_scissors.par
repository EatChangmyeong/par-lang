type Nat = recursive either { .zero!, .add1 self }

type Move = either {
  .rock!
  .paper!
  .scissors!
}

type Player = iterative/game choice {
  .stop => !
  .play_round => iterative/round choice {
    .stop_round => self/game,
    .play_move => (Move) choice {
      .win  => self/game,
      .lose => self/game,
      .draw => self/round,
    }
  }
}

type Outcome = either {
  .first_won!
  .second_won!
  .too_long!
}

type Game = iterative choice {
  .stop => !
  .play_round => (Outcome) self
}

dec StartGame : [Player, Player] Game
def StartGame = [p1, p2] begin/game case {
  .stop => do {
    p1.stop?
    p2.stop?
  } in !

  .play_round => do {
    let limit: Nat = .add1.add1.add1.zero!
    p1.play_round
    p2.play_round
  } in limit.begin/round.case {
    .zero! => do {
      p1.stop_round
      p2.stop_round
    } in (.too_long!) loop/game

    .add1 limit => do {
      p1.play_move[move1]
      p2.play_move[move2]
    } in CalculateRound(move1, move2).case {
      .first! => do {
        p1.win
        p2.lose
      } in (.first_won!) loop/game

      .second! => do {
        p1.lose
        p2.win
      } in (.second_won!) loop/game

      .draw! => do { p1.draw p2.draw } in limit.loop/round
    }
  }
}

dec CalculateRound : [Move, Move] either { .first!, .second!, .draw! }
def CalculateRound = [move1, move2] move1.case {
  .rock! => move2.case {
    .rock! => .draw!
    .paper! => .second!
    .scissors! => .first!
  }
  .paper! => move2.case {
    .rock! => .first!
    .paper! => .draw!
    .scissors! => .second!
  }
  .scissors! => move2.case {
    .rock! => .second!
    .paper! => .first!
    .scissors! => .draw!
  }
}
