def Main: ! = chan exit {
  let console = Console.Open

  catch ! => { console.print("Failed to read input.").close; exit! }
  console.prompt("Src path: ")[try src]
  console.prompt("Dst path: ")[try dst]

  catch e: Os.Error => {
    console.print("An error occurred:")
    console.print(e)
    console.close
    exit!
  }

  let try writer = Os.PathFromString(dst).createOrReplaceFile
  catch/r e => { writer.close(.ok!); throw e }

  let try/r reader = Os.PathFromString(src).openFile
  catch/w e => { reader.close(.ok!); throw e }

  reader.begin.read.try/r.case {
    .end! => {
      writer.close(.ok!).try
      console.close
      exit!
    }
    .chunk(bytes) => {
      writer.write(bytes).try/w
      reader.loop
    }
  }
}